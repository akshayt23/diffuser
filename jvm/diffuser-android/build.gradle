apply plugin: 'com.android.library'

apply from: rootProject.file('gradle/java_8_sources.gradle')

ext.compileUnitTestsAsJava8()

dependencies {
    api project(':diffuser')
    api 'androidx.annotation:annotation:1.1.0'
    api 'androidx.recyclerview:recyclerview:1.1.0'


    testImplementation "junit:junit:${versions.junit}"
}

static def gitSha() {
    return 'git rev-parse --short HEAD'.execute().text.trim()
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        buildConfigField "String", "LIBRARY_VERSION", "\"${VERSION_NAME}\""
        buildConfigField "String", "GIT_SHA", "\"${gitSha()}\""
    }
    lintOptions {
        disable 'InvalidPackage'
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
}

task sourceJar(type: Jar) {
    from 'src/main/java'
}

String sdkHome = System.getProperty("user.home") + '/.android-sdk/sdk/'
def licensesFolder = new File(sdkHome + "licenses")
if (!licensesFolder.exists()) {
    licensesFolder.mkdirs()
}

new File(sdkHome + 'licenses/android-sdk-license')
        .write("\nd56f5187479451eabf01fb78af6dfcb131a6481e")
new File(sdkHome + 'licenses/android-sdk-preview-license')
        .write("\n84831b9409646a918e30573bab4c9c91346d8abd")

task publish(dependsOn: uploadArchives) {}
